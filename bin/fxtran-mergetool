#!/usr/bin/perl -w

=head1 NAME

fxtran-mergetool

=head1 SYNOPSIS

  $ git mergetool -t fxtran-mergetool

=head1 DESCRIPTION

Wrap kdiff3, meld or vimdiff. Expand the following FORTRAN statetemnts before calling merge utilities:

- call
- associate
- subroutine

Repack after merging.

=head1 CONFIGURATION

Add this in your ~/.gitconfig :

  [mergetool "fxtran-mergetool"]
          cmd = fxtran-mergetool --mergetool kdiff3 "$BASE" "$LOCAL" "$REMOTE" "$MERGED"
          trustExitCode = true

=head1 AUTHOR

philippe.marguinaud@meteo.fr

=head1 SEE ALSO

fxtran

=cut

use Data::Dumper;
use Getopt::Long;
use FileHandle;
use FindBin qw ($Bin);

use lib "$Bin/../lib";

use Fxtran::Common;
use Fxtran::Bt;

use fxtran;
use fxtran::parser;
use fxtran::xpath;

use strict;

use Fxtran::Formatter;
use Fxtran::MergeTool;

my $log;

sub ll
{
  $log ||= 'FileHandle'->new (">>fxtran-mergetool.txt");

  my @call = caller (0);
  $log->print ("$call[1]:$call[2]\n");
  $log->print (@_);
  $log->print ("\n" x 2);
}

sub debugCommand
{
  use File::Temp;
  my %args = @_;

  my @cmd = @{ $args{cmd} };
  my $file = $args{file} || '';

  my $bashrc = 'File::Temp'->new (UNLINK => 1, SUFFIX => '.sh');

  $bashrc->print (<< "EOF");

alias r="echo \\"@cmd\\";  @cmd"

alias e="vi $file"

echo "Command \'@cmd\' fails:"
echo
@cmd
echo
echo "Type r for running command '@cmd'"
echo
if [ "x$file" != "x" ]
then
echo "Type e for editing '$file'"
fi
echo

export PS1='debug> '

EOF

  $bashrc->flush ();

  system (qw (xterm -e bash --init-file), $bashrc, '-i');
}

sub runCommand
{
  my %args = @_;

  my @cmd = @{ $args{cmd} };

  if (system (@cmd))
    {
      goto FAILED unless ($args{debug});

      &debugCommand (%args);

      system (@cmd)
        and goto FAILED;
    }

  return;
FAILED:
  die ("Command `@cmd' failed");
}

sub which
{
  my $prog = shift;
  for my $path (split (m/:/o, $ENV{PATH}))
    {
      my $p = "$path/$prog";
      return $p if ((-f $p) && (-x $p));
    }
}

my %opts = qw (simplify-associate-blocks 1);
my @opts_f = qw (simplify-associate-blocks help);
my @opts_s = qw (mergetool);

&GetOptions
(
  (map { ("$_!", \$opts{$_}) } @opts_f),
  (map { ("$_=s", \$opts{$_}) } @opts_s),
);

if ($opts{help} || (scalar (@ARGV) != 4))
  {
    exec ('perldoc', $0);
  }

unless ($opts{mergetool})
  {
    for my $prog (qw (kdiff3 meld vimdiff))
      {
        next unless (&which ($prog));
        $opts{mergetool} = $prog;
        last;
      }
  }

$opts{log} = \&ll;
$opts{runcommand} = \&runCommand;

my ($base, $local, $remote, $merged) = @ARGV;

'Fxtran::Formatter'->prepareFileForMerging ($_, %opts) 
  for ($base, $local, $remote);

'Fxtran::MergeTool'->merge ($base, $local, $remote, $merged, %opts);

'Fxtran::Formatter'->repackStatementsAfterMerge ($merged, %opts);

exit (0);

