SUBROUTINE MEANWIND_XFU (YDCPG_OPTS, YDCPG_BNDS, KMEANSTEPS, PXFU, PXFV, PXU, PXV, PMWINDCALC, ZUM, ZVM)

!=GENERATE= TARGET=SyncHost/SingleColumnFieldAPI/FieldAPI

USE PARKIND1         , ONLY : JPIM, JPRB
USE YOMHOOK          , ONLY : DR_HOOK, LHOOK

! The algorithm is the same as the one used to compute observed mean wind
USE CPG_OPTS_TYPE_MOD, ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE

IMPLICIT NONE

TYPE(CPG_OPTS_TYPE)    ,INTENT(IN)     :: YDCPG_OPTS
TYPE(CPG_BNDS_TYPE)    ,INTENT(IN)     :: YDCPG_BNDS
INTEGER(KIND=JPIM)     ,INTENT(IN)     :: KMEANSTEPS
REAL(KIND=JPRB)        ,INTENT(IN)     :: PXFU(YDCPG_OPTS%KLON)
REAL(KIND=JPRB)        ,INTENT(IN)     :: PXFV(YDCPG_OPTS%KLON)
REAL(KIND=JPRB)        ,INTENT(IN)     :: PXU(YDCPG_OPTS%KLON)
REAL(KIND=JPRB)        ,INTENT(IN)     :: PXV(YDCPG_OPTS%KLON)
REAL(KIND=JPRB)        ,INTENT(INOUT)  :: PMWINDCALC(YDCPG_OPTS%KLON)
REAL(KIND=JPRB)        ,INTENT(OUT)    :: ZUM(YDCPG_OPTS%KLON)
REAL(KIND=JPRB)        ,INTENT(OUT)    :: ZVM(YDCPG_OPTS%KLON)

REAL(KIND=JPRB) :: ZMEANMODULE(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZMEANANGLE
REAL(KIND=JPRB) :: ZANGLE
REAL(KIND=JPRB) :: ZMEANCANGLE
REAL(KIND=JPRB) :: ZMEANSANGLE
REAL(KIND=JPRB), PARAMETER :: ZEPS = 1.E-12_JPRB

INTEGER (KIND=JPIM) :: JLON

REAL(KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('MEANWIND_XFU', 0, ZHOOK_HANDLE)

IF (KMEANSTEPS==1) THEN

!=PARALLEL

  ZUM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=PXU(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)
  ZVM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=PXV(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)
  PMWINDCALC(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=1.0_JPRB

!=END PARALLEL

ELSE

!=PARALLEL

  DO JLON=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
    ! Module of mean wind is the mean of the modules
    ZMEANMODULE(JLON)=SQRT(PXFU(JLON)**2+PXFV(JLON)**2)
  ENDDO
  DO JLON=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
    ! Module of mean wind is the mean of the modules
    ZMEANMODULE(JLON)= ZMEANMODULE(JLON)*REAL(KMEANSTEPS-1,JPRB) + SQRT(PXU(JLON)**2+PXV(JLON)**2)
  ENDDO
  DO JLON=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
    ! We compute mean of COS and mean of SIN
    ! Direction of mean wind is the direction of vector [mean COS, mean SIN]
    IF (ABS(PXFV(JLON))<ZEPS.AND.ABS(PXFU(JLON))<ZEPS) THEN
      ZMEANANGLE=0.0_JPRB
    ELSE
      ZMEANANGLE=ATAN2(PXFV(JLON),PXFU(JLON))
    ENDIF
    IF (ABS(PXV(JLON))<ZEPS.AND.ABS(PXU(JLON))<ZEPS) THEN
      ZANGLE=0.0_JPRB
    ELSE
      ZANGLE=ATAN2(PXV(JLON),PXU(JLON))
    ENDIF
    ZMEANCANGLE=( COS(ZMEANANGLE)*REAL(KMEANSTEPS-1,JPRB) * &
     & PMWINDCALC(JLON)+ &
     & COS(ZANGLE) ) / REAL(KMEANSTEPS,JPRB)
    ZMEANSANGLE=( SIN(ZMEANANGLE)*REAL(KMEANSTEPS-1,JPRB) * &
     & PMWINDCALC(JLON)+ &
     & SIN(ZANGLE) ) / REAL(KMEANSTEPS,JPRB)
    PMWINDCALC(JLON)=SQRT(ZMEANCANGLE**2+ZMEANSANGLE**2)
    IF (ABS(ZMEANSANGLE)<ZEPS.AND.ABS(ZMEANCANGLE)<ZEPS) THEN
      ZMEANANGLE=0.0_JPRB
    ELSE
      ZMEANANGLE=ATAN2(ZMEANSANGLE,ZMEANCANGLE)
    ENDIF
    ZUM(JLON)=ZMEANMODULE(JLON)*COS(ZMEANANGLE)/ REAL(KMEANSTEPS,JPRB)
    ZVM(JLON)=ZMEANMODULE(JLON)*SIN(ZMEANANGLE)/ REAL(KMEANSTEPS,JPRB)
  ENDDO

!=END PARALLEL

ENDIF

IF (LHOOK) CALL DR_HOOK('MEANWIND_XFU', 1, ZHOOK_HANDLE)

END SUBROUTINE MEANWIND_XFU
