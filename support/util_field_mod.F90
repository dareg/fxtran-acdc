MODULE UTIL_FIELD_MOD

USE FIELD_MODULE

IMPLICIT NONE

INTERFACE LOAD
  MODULE PROCEDURE LOAD_FIELD_2D
  MODULE PROCEDURE LOAD_FIELD_3D
  MODULE PROCEDURE LOAD_FIELD_4D
END INTERFACE

INTERFACE SAVE
  MODULE PROCEDURE SAVE_FIELD_2D
  MODULE PROCEDURE SAVE_FIELD_3D
  MODULE PROCEDURE SAVE_FIELD_4D
END INTERFACE

INTERFACE DIFF
  MODULE PROCEDURE DIFF_FIELD_2D
  MODULE PROCEDURE DIFF_FIELD_3D
  MODULE PROCEDURE DIFF_FIELD_4D
END INTERFACE

CONTAINS

SUBROUTINE LOAD_FIELD_2D (KLUN, YD)
INTEGER (KIND=JPIM), INTENT (IN) :: KLUN
CLASS (FIELD_2D), POINTER :: YD
INTEGER (KIND=JPIM) :: ISIZE (2), LBOUNDS (2), UBOUNDS (2)

TYPE (FIELD_2D_OWNER), POINTER :: YL

ALLOCATE (YL)

READ (KLUN) LBOUNDS
READ (KLUN) ISIZE
UBOUNDS = LBOUNDS + ISIZE - 1
ALLOCATE (YL%PTR (LBOUNDS (1):UBOUNDS (1),LBOUNDS (2):UBOUNDS (2)))
READ (KLUN) YL%PTR

YD => YL

END SUBROUTINE

SUBROUTINE LOAD_FIELD_3D (KLUN, YD)
INTEGER (KIND=JPIM), INTENT (IN) :: KLUN
CLASS (FIELD_3D), POINTER :: YD

INTEGER (KIND=JPIM) :: ISIZE (3), LBOUNDS (3), UBOUNDS (3)

TYPE (FIELD_3D_OWNER), POINTER :: YL

ALLOCATE (YL)

READ (KLUN) LBOUNDS
READ (KLUN) ISIZE
UBOUNDS = LBOUNDS + ISIZE - 1
ALLOCATE (YL%PTR (LBOUNDS (1):UBOUNDS (1),LBOUNDS (2):UBOUNDS (2),LBOUNDS (3):UBOUNDS (3)))
READ (KLUN) YL%PTR

YD => YL

END SUBROUTINE

SUBROUTINE LOAD_FIELD_4D (KLUN, YD)
INTEGER (KIND=JPIM), INTENT (IN) :: KLUN
CLASS (FIELD_4D), POINTER :: YD

INTEGER (KIND=JPIM) :: ISIZE (4), LBOUNDS (4), UBOUNDS (4)

TYPE (FIELD_4D_OWNER), POINTER :: YL

ALLOCATE (YL)

READ (KLUN) LBOUNDS
READ (KLUN) ISIZE
UBOUNDS = LBOUNDS + ISIZE - 1
ALLOCATE (YL%PTR (LBOUNDS (1):UBOUNDS (1),LBOUNDS (2):UBOUNDS (2),LBOUNDS (3):UBOUNDS (3),LBOUNDS (4):UBOUNDS (4)))
READ (KLUN) YL%PTR

YD => YL

END SUBROUTINE

SUBROUTINE SAVE_FIELD_2D (KLUN, YD)
INTEGER (KIND=JPIM), INTENT (IN) :: KLUN
CLASS (FIELD_2D), POINTER :: YD
INTEGER (KIND=JPIM) :: ISIZE (2), LBOUNDS (2), UBOUNDS (2)

LBOUNDS = LBOUND (YD%PTR)
UBOUNDS = UBOUND (YD%PTR)
ISIZE = UBOUNDS - LBOUNDS + 1

WRITE (KLUN) LBOUNDS
WRITE (KLUN) ISIZE
WRITE (KLUN) YD%PTR

END SUBROUTINE

SUBROUTINE SAVE_FIELD_3D (KLUN, YD)
INTEGER (KIND=JPIM), INTENT (IN) :: KLUN
CLASS (FIELD_3D), POINTER :: YD
INTEGER (KIND=JPIM) :: ISIZE (3), LBOUNDS (3), UBOUNDS (3)

LBOUNDS = LBOUND (YD%PTR)
UBOUNDS = UBOUND (YD%PTR)
ISIZE = UBOUNDS - LBOUNDS + 1

WRITE (KLUN) LBOUNDS
WRITE (KLUN) ISIZE
WRITE (KLUN) YD%PTR

END SUBROUTINE

SUBROUTINE SAVE_FIELD_4D (KLUN, YD)
INTEGER (KIND=JPIM), INTENT (IN) :: KLUN
CLASS (FIELD_4D), POINTER :: YD
INTEGER (KIND=JPIM) :: ISIZE (4), LBOUNDS (4), UBOUNDS (4)

LBOUNDS = LBOUND (YD%PTR)
UBOUNDS = UBOUND (YD%PTR)
ISIZE = UBOUNDS - LBOUNDS + 1

WRITE (KLUN) LBOUNDS
WRITE (KLUN) ISIZE
WRITE (KLUN) YD%PTR

END SUBROUTINE

SUBROUTINE DIFF_FIELD_2D (CDMESS, YD, YO)
CHARACTER(LEN=*), INTENT(IN) :: CDMESS
CLASS (FIELD_2D), POINTER :: YD, YO

INTEGER (KIND=JPIM) :: I1, I2

IF (ANY (LBOUND (YD%PTR) /= LBOUND (YO%PTR))) CALL ABOR1 ('DIMENSION MISMATCH')
IF (ANY (UBOUND (YD%PTR) /= UBOUND (YO%PTR))) CALL ABOR1 ('DIMENSION MISMATCH')

DO I2 = LBOUND (YD%PTR, 2), UBOUND (YD%PTR, 2)
DO I1 = LBOUND (YD%PTR, 1), UBOUND (YD%PTR, 1)
  IF (YD%PTR (I1, I2) /= YO%PTR (I1, I2)) THEN
    WRITE (*, '(A16," ",2I4," ",3E15.4)') CDMESS, I1, I2, YD%PTR (I1, I2), YO%PTR (I1, I2), YD%PTR (I1, I2) - YO%PTR (I1, I2)
  ENDIF
ENDDO
ENDDO

END SUBROUTINE

SUBROUTINE DIFF_FIELD_3D (CDMESS, YD, YO)
CHARACTER(LEN=*), INTENT(IN) :: CDMESS
CLASS (FIELD_3D), POINTER :: YD, YO

INTEGER (KIND=JPIM) :: I1, I2, I3

IF (ANY (LBOUND (YD%PTR) /= LBOUND (YO%PTR))) CALL ABOR1 ('DIMENSION MISMATCH')
IF (ANY (UBOUND (YD%PTR) /= UBOUND (YO%PTR))) CALL ABOR1 ('DIMENSION MISMATCH')

DO I3 = LBOUND (YD%PTR, 3), UBOUND (YD%PTR, 3)
DO I2 = LBOUND (YD%PTR, 2), UBOUND (YD%PTR, 2)
DO I1 = LBOUND (YD%PTR, 1), UBOUND (YD%PTR, 1)
  IF (YD%PTR (I1, I2, I3) /= YO%PTR (I1, I2, I3)) THEN
    WRITE (*, '(A16," ",3I4," ",3E15.4)') CDMESS, I1, I2, I3, YD%PTR (I1, I2, I3), YO%PTR (I1, I2, I3), YD%PTR (I1, I2, I3) - YO%PTR (I1, I2, I3)
  ENDIF
ENDDO
ENDDO
ENDDO

END SUBROUTINE

SUBROUTINE DIFF_FIELD_4D (CDMESS, YD, YO)
CHARACTER(LEN=*), INTENT(IN) :: CDMESS
CLASS (FIELD_4D), POINTER :: YD, YO

INTEGER (KIND=JPIM) :: I1, I2, I3, I4

IF (ANY (LBOUND (YD%PTR) /= LBOUND (YO%PTR))) CALL ABOR1 ('DIMENSION MISMATCH')
IF (ANY (UBOUND (YD%PTR) /= UBOUND (YO%PTR))) CALL ABOR1 ('DIMENSION MISMATCH')

DO I4 = LBOUND (YD%PTR, 4), UBOUND (YD%PTR, 4)
DO I3 = LBOUND (YD%PTR, 3), UBOUND (YD%PTR, 3)
DO I2 = LBOUND (YD%PTR, 2), UBOUND (YD%PTR, 2)
DO I1 = LBOUND (YD%PTR, 1), UBOUND (YD%PTR, 1)
  IF (YD%PTR (I1, I2, I3, I4) /= YO%PTR (I1, I2, I3, I4)) THEN
    WRITE (*, '(A16," ",4I4," ",3E15.4)') CDMESS, I1, I2, I3, I4, YD%PTR (I1, I2, I3, I4), YO%PTR (I1, I2, I3, I4), YD%PTR (I1, I2, I3, I4) - YO%PTR (I1, I2, I3, I4)
  ENDIF
ENDDO
ENDDO
ENDDO
ENDDO

END SUBROUTINE

END MODULE
